apply plugin: 'bintray'
apply plugin: 'maven'

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

configurations { archives }

artifacts {
    archives sourcesJar, javadocJar
}

task createPom << {
    pom {
        artifactId = project.archivesBaseName
        project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }.writeTo("$buildDir/pom.xml")
}

bintray {
    if (project.hasProperty('bintrayUser')) {
        user = bintrayUser
    }

    if (project.hasProperty('bintrayKey')) {
        key = bintrayKey
    }
    configurations = ['archives']
    pkg {
        repo = 'open-source'
        userOrg = 'equalexperts'
        name = getProject().archivesBaseName
        licenses = ['Apache-2.0']
    }
}

bintrayUpload.dependsOn createPom

install {
    description = 'Install artifacts into the local maven repository.'
    group = "Maven"
}