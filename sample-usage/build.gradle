apply plugin: 'java'
apply from: rootProject.file("jacoco-support.gradle")

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

configurations {
    provided {
        dependencies.all { dep ->
            configurations.default.exclude group: dep.group, module: dep.name
        }
    }
    compile.extendsFrom provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

dependencies {
    compile project(":opslogger"), spring, dagger
    provided daggerCompiler
    testCompile project(":opslogger-support"), junit, mockito
}

group = 'com.equalexperts'
version = rootProject.version

task generateLoggingDocumentation {
    String outputFile = "${buildDir}/log-messages.txt"
    doLast {
        javaexec {
            classpath sourceSets.test.runtimeClasspath
            main = 'com.equalexperts.logging.GenerateLogMessageDocumentation'
            args = [outputFile, 'build/classes/main']
        }
    }

    dependsOn(compileJava, compileTestJava)
    inputs.files sourceSets.main.allSource
    inputs.files sourceSets.test.runtimeClasspath
    outputs.file outputFile
}
assemble.dependsOn(generateLoggingDocumentation)

idea.springFacets << 'src/main/resources/applicationContext.xml'
